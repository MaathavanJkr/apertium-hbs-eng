Alphabet
! Yksittäiset merkit:
 	a %{A%} %{Â%} b c ć č d đ dž e %{E%} f g %{G%} h %{H%} i j %{J%} k %{K%} l lj m n nj o p r s š t u v z ž 
%>:0 %  ;
 	
Rule-variables
	Cx Cy Cz ;

Sets
	Vowel = a %{A%} e %{E%} i o u ;
	Cons =  b c č ć d đ f g %{G%} h %{H%} j %{J%} k %{K%} l lj m n nj o p r s š t v z ž ;
	VcedObs = b đ dž d g z ž ;
	VlessObs = p ć č t k s š ;
	Hard = t d s z g k h l n ;
	Soft = ć đ š ž č lj nj j ;

Rules

"1. palatalisation"
Cx:Cy <=> _ i:i ;
	where	Cx in	(%{K%} %{G%} %{H%})
		Cy in	( c   z   s ) matched ;

"No palatalisation"
Cx:Cy <=> _ \i:i ;
	where	Cx in	(%{K%} %{G%} %{H%})
		Cy in	( k   g   h ) matched ;

"Iotation - lateralisation"
%{J%}:lj <=> [ b: | p: | m: | v: ] _ ;

"Iotation - palatalisation"
Cx:Cy <=> _ %{J%}: ;
	where	Cx in	( t d s z g k h l  n )
		Cy in	( ć đ š ž ž č š lj nj ) matched ;

"No iotation"
%{J%}:j <=> [ č | ž | r | š ] _ ;

"Elision of J"
%{J%}:0 <=> Hard:Soft _ ;

"Elision of A"
%{A%}:0 <=> _ :Cons Vowel: ;

"Vocalisation of A"
%{A%}:a <=> _ [ :Cons .#. | :Cons %{Â%}:a ] ;

"Word-final %{Â%} in gen.pl."
%{Â%}:a <=> _ ;

"Soft E"
%{E%}:e <=> [ c: | Soft: ] _ ;

"Hard E"
%{E%}:o <=> \[ c: | Soft: ] _ ;

"Voicing assimilation 1"
Cx:Cy <=> _ :VlessObs ;
	where	Cx in	(b đ dž d g z ž)
		Cy in	(p ć č  t k s š) matched ;
			
"Voicing assimilation 2"
Cx:Cy <=> _ :VcedObs ;
	where	Cx in	(p ć č  t k s š)
		Cy in	(b đ dž d g z ž) matched ;