DELIMITERS = "<.>" "<!>" "<?>" ;
SOFT-DELIMITERS = "<,>" ;

LIST BOS = (>>>) ; # Beginning of sentence
LIST EOS = (<<<) ; # End of sentence

LIST Nom = nom ;
LIST Name = name ;
LIST Acc = acc ;
LIST Ins = ins ;
LIST Gen = gen ;
LIST Voc = voc ;
LIST Noun = n ;
LIST Adj = adj ;
LIST Masc = m ;
LIST Fem = f ;
LIST Neut = nt ;
LIST Sg = sg ;
LIST Pl = pl ;
LIST Verb = v ;
LIST Pres = pres ;
LIST Past = past ;
LIST Vtrans = tv ;

LIST CASE = nom acc gen voc ;
LIST NUMBER = sg pl ;
LIST GENDER = m f nt ;
LIST COPULA = "je" "su" "sam" ;
LIST IMATI = "ima" "imaju" ;
LIST NEMA = "nema" ;
LIST Gen-Prep = "od" "do" "prije" "poslije" "iza" "blizu" "ispred" ;
LIST Acc-Prep = "za" "kroz" ;
LIST Prep = "na" "u" "kroz" ;
LIST Ins-Prep = "s" "sa" ;

SET Adj-in-Gen = Adj + Gen ;
SET Acc-or-Nom = Acc | Nom ;
SET Gen-or-Nom = Gen | Nom ;

SECTION

# Select the genitive reading if the preceding adjective is in genitive
SELECT Gen IF	(-1C Adj-in-Gen) ;	

# Select the genitive reading if the preceding preposition governs the genetive
SELECT Gen IF	(-1C Gen-Prep) ;	

# Select the genitive reading if the preceding preposition governs the genetive
SELECT Acc IF	(-1C Acc-Prep) ;

# Select the accusative reading if the preceding word is a transitive verb
REMOVE Acc IF	(NOT -1* Vtrans) ;	


# Gender agreement
SELECT $$GENDER if (0C Adj) (1C Noun + $$GENDER) ;
SELECT $$GENDER if (0C Adj) (1C Adj + $$GENDER) ;


# Number agreement
SELECT $$NUMBER if (0C Noun) (-1C Adj + $$NUMBER) ;

# Case agreement
SELECT $$CASE if (0C Noun) (-1C Adj + $$CASE) ;		
SELECT $$CASE if (0C Adj ) (-1C Adj + $$CASE) ;

# Preposition + NOT-Nominative
REMOVE Nom IF	(-1C Prep ) ;
REMOVE Voc IF	(-1C Prep ) ;

# Prefer nominative singular over genitive plural for proper nouns
SELECT Sg IF	(0C Name ) ;

SELECT Ins IF	(-1C Ins-Prep ) ;
